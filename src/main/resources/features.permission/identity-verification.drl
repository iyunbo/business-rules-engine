package org.drools.workshop;

import org.drools.workshop.model.Person;
import org.drools.workshop.model.Person.Employment;
import java.util.List;
import org.drools.workshop.service.*;

global ReportService reportService;

declare AgeIsValid
    person: Person @key
end

declare EmploymentIsValid
    person: Person @key
end

declare IdentityIsValid
    person: Person @key
end

rule "Person with age between 20 and 50"
    when
        $p: Person(age >= 20, age <= 50)
    then
        reportService.notify($p.getName() + " - 居住地符合要求");
        insertLogical(new AgeIsValid($p));
end

rule "Person who is white color"
    when
        $pWithAgeOK: AgeIsValid(person.employment == Employment.WhiteColor)
    then
        reportService.notify($pWithAgeOK.getPerson().getName() + " -- 居住地和职业符合要求");
        insertLogical(new EmploymentIsValid($pWithAgeOK.getPerson()));
end

rule "Person who is local resident"
    when
        $pWithEmploymentOK: EmploymentIsValid(person.localResident)
    then
        reportService.notify($pWithEmploymentOK.getPerson().getName() + " --- 身份符合要求： " + $pWithEmploymentOK.getPerson());
        insertLogical(new IdentityIsValid($pWithEmploymentOK.getPerson()));
end